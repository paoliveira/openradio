apply plugin: "com.google.gms.google-services"
apply plugin: "com.android.application"
apply plugin: "dagger.hilt.android.plugin"
apply plugin: "com.google.firebase.crashlytics"
apply from: "constants.gradle"

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    def versionPropsFile = file('version.properties')
    def code = 1
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        code = versionProps['VERSION_CODE'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties !")
    }
    println "Version code:" + code

    signingConfigs {
        config {
            keyAlias project.ext.aliasProp
            keyPassword project.ext.pwdProp
            storeFile file(project.ext.storeFileProp)
            storePassword project.ext.pwdProp
        }
    }

    defaultConfig {
        applicationId project.ext.applicationId
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode code
        versionName project.ext.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }
        release {
            // TODO:
//            minifyEnabled true
//            shrinkResources true
            signingConfig signingConfigs.config
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    aaptOptions {
        cruncherEnabled = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    api "com.google.android.gms:play-services-cronet:$googlePlayServicesCronet"

    implementation "org.checkerframework:checker-compat-qual:$checkerCompatQualVersion"
    implementation "org.checkerframework:checker-qual:$checkerQualVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "androidx.leanback:leanback:$leanbackVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "log4j:log4j:$log4jVersion"
    implementation "com.google.firebase:firebase-installations:$firebaseInstallationsVersion"
    implementation "com.google.firebase:firebase-crashlytics:$firebaseCrashlyticsVersion"
    implementation ("com.google.firebase:firebase-analytics:$firebaseVersion") {
        exclude group: "com.google.android.gms", module: "play-services-ads-identifier"
    }
    implementation "com.google.android.gms:play-services-location:$googlePlayServicesLocationVersion"
    implementation "com.google.android.gms:play-services-auth:$googlePlayServicesAuthVersion"
    implementation "com.google.android.gms:play-services-drive:$googlePlayServicesDriveVersion"
    // https://mvnrepository.com/artifact/org.jdom/jdom2
    implementation group: "org.jdom", name: "jdom2", version: "$jdom2Version"
    implementation "de.westnordost:countryboundaries:$countryboundariesVersion"
    implementation "com.android.support:multidex:$multidexVersion"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoPlayerVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleExtensionVersion"
    implementation "com.google.dagger:hilt-android:$hiltAndroidVersion"
    annotationProcessor "com.google.dagger:hilt-android-compiler:$hiltAndroidCompilerVersion"

    implementation "com.google.http-client:google-http-client-gson:$googleHttpClientGsonVersion"
    implementation("com.google.api-client:google-api-client-android:$googleApiClientAndroidVersion") {
        exclude group: "org.apache.httpcomponents"
    }
    implementation("com.google.apis:google-api-services-drive:$googleApiServicesDrive") {
        exclude group: "org.apache.httpcomponents"
    }

    implementation "com.squareup.picasso:picasso:$picassoVersion"

    androidTestImplementation(
            "androidx.test.espresso:espresso-core:$espressoVersion",
            {exclude group: "com.android.support", module: "support-annotations"}
    )
    androidTestImplementation "androidx.test.ext:junit:$junitVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation group: "org.json", name: "json", version: "$jsonTestVersion"
}

tasks.whenTaskAdded { task ->
    if (task.name == "signReleaseBundle") {
        task.doLast() {
            println "Get And Increment Version code"

            def versionPropsFile = file('version.properties')
            if (versionPropsFile.canRead()) {
                Properties versionProps = new Properties()
                versionProps.load(new FileInputStream(versionPropsFile))
                def code = versionProps['VERSION_CODE'].toInteger() + 1
                versionProps['VERSION_CODE'] = code.toString()
                versionProps.store(versionPropsFile.newWriter(), null)

                println "Version code incremented to " + code
            } else {
                throw new GradleException("Could not read version.properties !")
            }
        }
    }
}
