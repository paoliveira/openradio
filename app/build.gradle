apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: '../constants.gradle'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    def versionPropsFile = file('version.properties')
    def code = 1
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        code = versionProps['VERSION_CODE'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties !")
    }
    println "Version code:" + code

    def signConfFile = file("sign.properties")
    def storeFileProp
    def aliasProp
    def pwdProp
    if (signConfFile.canRead()) {
        Properties signProps = new Properties()
        signProps.load(new FileInputStream(signConfFile))
        storeFileProp = signProps['file'].toString()
        aliasProp = signProps['alias'].toString()
        pwdProp = signProps['pwd'].toString()
    } else {
        throw new GradleException("Could not read sign.properties !")
    }

    signingConfigs {
        config {
            keyAlias aliasProp
            keyPassword pwdProp
            storeFile file(storeFileProp)
            storePassword pwdProp
        }
    }

    defaultConfig {
        applicationId project.ext.applicationId
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode code
        versionName project.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        final def crashlyticsApiKeyFile = file('crashlytics.properties')
        if (crashlyticsApiKeyFile.canRead()) {
            final Properties properties = new Properties()
            properties.load(new FileInputStream(crashlyticsApiKeyFile))
            def apiKey = properties['apiKey'].toString()
            println "Crashlytics API key " + apiKey

            manifestPlaceholders = [crashlytics_api_key: apiKey]
        } else {
            throw new GradleException("Could not read crashlytics.properties !")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }
        release {
            signingConfig signingConfigs.config
        }
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "28.+"
            }
        }
        if (requested.group == "androidx") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "${targetSdk}.+"
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation "com.android.support:support-v4:$supportVersion"
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") {
        transitive = true
    }
    implementation "com.google.android.gms:play-services-auth:$googlePlayServicesAuthVersion"
    implementation "com.google.android.gms:play-services-drive:$googlePlayServicesDriveVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "log4j:log4j:$log4jVersion"
    // https://mvnrepository.com/artifact/org.jdom/jdom2
    implementation group: 'org.jdom', name: 'jdom2', version: '2.0.6'

    compileOnly 'org.checkerframework:checker-qual:' + checkerframeworkVersion

    androidTestImplementation(
            'com.android.support.test.espresso:espresso-core:2.2.2',
            {exclude group: 'com.android.support', module: 'support-annotations'}
    )

    testImplementation "junit:junit:$junitVersion"
    testImplementation group: "org.json", name: "json", version: "$jsonTestVersion"
}

tasks.whenTaskAdded { task ->
    if (task.name == "assembleRelease") {
        task.doLast() {
            println "Get And Increment Version code"

            def versionPropsFile = file('version.properties')
            if (versionPropsFile.canRead()) {
                Properties versionProps = new Properties()
                versionProps.load(new FileInputStream(versionPropsFile))
                def code = versionProps['VERSION_CODE'].toInteger() + 1
                versionProps['VERSION_CODE'] = code.toString()
                versionProps.store(versionPropsFile.newWriter(), null)

                println "Version code incremented to " + code
            } else {
                throw new GradleException("Could not read version.properties !")
            }
        }
    }
}
