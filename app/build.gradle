apply plugin: "com.google.gms.google-services"
apply plugin: "com.android.application"
apply plugin: "dagger.hilt.android.plugin"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: "kotlin-android"
apply plugin: 'kotlin-kapt'
apply from: "../constants.gradle"

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    def versionPropsFile = file('version.properties')
    def code = 1
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        code = versionProps['VERSION_CODE'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties !")
    }
    println "Version code:" + code

    /**
     * Sign
     */
    def storeFileProp = ""
    def aliasProp = ""
    def pwdProp = ""

    /**
     * Load constants from files
     */
    def signConfFile = file("sign.properties")
    if (signConfFile.canRead()) {
        Properties signProps = new Properties()
        signProps.load(new FileInputStream(signConfFile))
        storeFileProp = signProps['file'].toString()
        aliasProp = signProps['alias'].toString()
        pwdProp = signProps['pwd'].toString()
    } else {
        throw new GradleException("Could not read sign.properties !")
    }

    signingConfigs {
        config {
            keyAlias aliasProp
            keyPassword pwdProp
            storeFile file(storeFileProp)
            storePassword pwdProp
        }
    }

    defaultConfig {
        applicationId project.ext.applicationId
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode code
        versionName project.ext.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
//            minifyEnabled true
//            shrinkResources true
            signingConfig signingConfigs.config
        }
        release {
//            minifyEnabled true
//            shrinkResources true
            signingConfig signingConfigs.config
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    aaptOptions {
        cruncherEnabled = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    api "com.google.android.gms:play-services-cronet:$googlePlayServicesCronet"

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.core:core-ktx:$kotlinCoreKtx"
    implementation "androidx.leanback:leanback:$leanbackVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinxCoroutinesAndroid"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesCore"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.checkerframework:checker-qual:$checkerQualVersion"

    implementation "com.android.support:multidex:$multidexVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    // Import the Firebase BoM (see: https://firebase.google.com/docs/android/learn-more#bom)
    implementation platform("com.google.firebase:firebase-bom:$firebaseBom")
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.google.android.gms:play-services-location:$googlePlayServicesLocationVersion"
    implementation "com.google.android.gms:play-services-auth:$googlePlayServicesAuthVersion"
    implementation "com.google.android.gms:play-services-drive:$googlePlayServicesDriveVersion"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-dash:$exoPlayerVersion"
    implementation "com.google.dagger:hilt-android:$hiltAndroidVersion"
    implementation "com.google.http-client:google-http-client-gson:$googleHttpClientGsonVersion"
    implementation("com.google.api-client:google-api-client-android:$googleApiClientAndroidVersion") {
        exclude group: "org.apache.httpcomponents"
    }
    implementation("com.google.apis:google-api-services-drive:$googleApiServicesDrive") {
        exclude group: "org.apache.httpcomponents"
    }

    // https://mvnrepository.com/artifact/org.jdom/jdom2
    implementation group: "org.jdom", name: "jdom2", version: "$jdom2Version"

    implementation "log4j:log4j:$log4jVersion"
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "de.westnordost:countryboundaries:$countryboundariesVersion"

    kapt "com.google.dagger:hilt-android-compiler:$hiltAndroidCompilerVersion"

    androidTestImplementation(
            "androidx.test.espresso:espresso-core:$espressoVersion",
            {exclude group: "com.android.support", module: "support-annotations"}
    )
    androidTestImplementation "androidx.test.ext:junit:$junitAndroidXVersion"

    // https://mvnrepository.com/artifact/junit/junit
    testImplementation group: "junit", name: "junit", version: "$junitVersion"
    testImplementation group: "org.json", name: "json", version: "$jsonTestVersion"
}

tasks.whenTaskAdded { task ->
    if (task.name == "signReleaseBundle") {
        task.doLast() {
            println "Get And Increment Version code"

            def versionPropsFile = file('version.properties')
            if (versionPropsFile.canRead()) {
                Properties versionProps = new Properties()
                versionProps.load(new FileInputStream(versionPropsFile))
                def code = versionProps['VERSION_CODE'].toInteger() + 1
                versionProps['VERSION_CODE'] = code.toString()
                versionProps.store(versionPropsFile.newWriter(), null)

                println "Version code incremented to " + code
            } else {
                throw new GradleException("Could not read version.properties !")
            }
        }
    }
}
